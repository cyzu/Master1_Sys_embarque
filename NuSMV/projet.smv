---------------------------------------------------
-- Projet NuSMV
-- Gérer les feux de piétons et feux de voiture
-- en assurant la sureté et la vivacité
--
-- Chloé Bensoussan
-- Université Nice Sophia-Antipolis
-- Master 1 Informatique
---------------------------------------------------


---------------------------------------------------
-- module pedestrian
-- change le feu des pietons en fonction du feux de voiture
---------------------------------------------------

MODULE pedestrian(car_red_bool, car_yellow_bool)
VAR
    light : {red, green};
    
ASSIGN
    init(light) := green; 
    next(light) := case
                        car_red_bool : red;
                        car_yellow_bool : green;
                        TRUE : light;
                   esac;


---------------------------------------------------
-- module compteur de tick
-- retourne VRAI si time ait atteint, FAUX sinon
---------------------------------------------------

MODULE counter_tick(time, activate)
VAR 
    tick : 1..time;
    
ASSIGN
    init(tick) := 1;
    next(tick) := case
                    tick = time : 1;
                    activate : tick + 1;
                    TRUE : 1;
                  esac;
DEFINE
    finish := tick = time;


---------------------------------------------------
-- module main
-- change le feu de voiture lorsque le compteur atteint le temps voulu
---------------------------------------------------
    
MODULE main
VAR 
    car_light : {red, yellow, green};
    
    red_timer : counter_tick(5, car_light = red);
    green_timer : counter_tick(10, car_light = green);
    yellow_timer : counter_tick(2, car_light = yellow);
    
    pedestrian_light : pedestrian(red_timer.finish, yellow_timer.finish);
    
ASSIGN
    init(car_light) := red;
    next(car_light) := case
                            red_timer.finish : green;
                            green_timer.finish : yellow;
                            yellow_timer.finish : red;
                            TRUE : car_light;
                       esac;
     
     -- Expressions LTL sureté et vivacité
     LTLSPEC G (pedestrian_light.light = green -> car_light = red)
     LTLSPEC F (car_light = green) <-> F (car_light = red)
     LTLSPEC F (pedestrian_light.light = green) <-> F (pedestrian_light.light = red)

     -- Expressions CTL sureté et vivacité


---------------------------------------------------
-- EXECUTION
-- ./NuSMV -int fichier.smv
-- go
-- pick_state -r
-- simulate -r -k pas
-- show_trace -v
-- quit
---------------------------------------------------
